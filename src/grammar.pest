module_declaration = { SOI ~ "module" ~ name ~ variable_declaration_list ~ "->" ~ variable_declaration_list ~ block ~ EOI }

name = { (ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")* }

variable_declaration_list = { "(" ~ (variable_declaration)* ~ ("," ~ variable_declaration)* ~ ")" }
variable_declaration = { variable_type ~ name }
variable_type = { bit_type }
bit_type = { "bit" }

block = { "{" ~ statement* ~ "}" }

indexed_variable = { name ~ "[" ~ variable_index ~ "]" }
variable_index = { "t" ~ (( addition | subtraction ) ~ pos_integer)? }
pos_integer = ${ ASCII_DIGIT+ }

statement = _{ assignment | declaration_statement }
declaration_statement = _{ variable_declaration ~ ";" }
assignment = { indexed_variable ~ "=" ~ expression ~ ";" }
expression = _{ binary_operation | indexed_variable | unary_operation | parenthetical_expression }
parenthetical_expression = { "(" ~ expression ~ ")" }

unary_operation = { unary_operator ~ expression }
unary_operator = { bitwise_inverse }
bitwise_inverse = { "~" }

binary_operand = _{ parenthetical_expression | indexed_variable | unary_operation }
binary_operation = { binary_operand ~ (binary_operator ~ binary_operand)+ }
binary_operator = _{ addition | subtraction }
addition = { "+" }
subtraction = { "-" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!"\n" ~ ANY)* }